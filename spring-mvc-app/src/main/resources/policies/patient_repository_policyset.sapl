// Import the filter library, so that 'blacken' can be used directly instead of using the absolute name 'filter.blacken'.
import filter.*

set "PatientRepository"

/*
 * The 'first-applicable' combination algorithm is used here in oder to avoid 'transformation uncertainty',
 * i.e., multiple policies which return PERMIT but do not agree about transformation of the resource.
 * This algorithm evaluates policies from top to bottom in the document and stops as soon as one policy 
 * yields an applicable result or errors.
 */
first-applicable

/*
 * This 'for' statement ensures, that the policies are only checked when they are actions, e.g., method calls,
 * in the designated Java class.
 */
for action.java.declaringTypeName == "org.demo.domain.PatientRepository"

/*
 * All doctors and nurses have full read access on all patient records.
 */
policy "doctor and nurse access to patient data"
permit 
       action.java.shortSig == "PatientRepository.findById(..)"
where 
       "DOCTOR" in subject..authority || "NURSE" in subject..authority; 

/*
 * Administrators read access patients, however the classification and diagnosis are blackened in parts
 * also administrator access is to be documented.
 */
policy "administrator access to patient data"
permit 
       action.java.shortSig == "PatientRepository.findById(..)"
where 
       "ADMIN" in subject..authority;
obligation  
		{ 
        	"type" : "logAccess",
			"message" : subject.name + " has accessed patient data (id="+resource.id+") as an administrator."
		}  
transform 
		// filtering with text blackening
		resource |- { 
						@.icd11Code : blacken(2,0,"\u2588"),
						@.diagnosisText : blacken(0,0,"\u2588") 
					}

/*
 * Visitors which are relatives may see phone number.
 */
policy "visiting relatives access patient data"
permit 
       action.java.shortSig == "PatientRepository.findById(..)"
where 
       "VISITOR" in subject..authority;
        /*
         * The next condition invokes the "patient" policy information point and 
         * determines the "relatives" attribute of id of the patient.
         * The policy information policy point accesses the database to determine 
         * the relatives of the patient and it is checked if the subject is in the 
         * list of relatives.
         */
       subject.name in resource.id.<patient.relatives>; 
transform 
		// Subtractive template with filters removing content
		resource |- { 
						@.icd11Code 			: remove,
						@.diagnosisText 		: remove,
						@.medicalRecordNumber 	: remove, 
						@.attendingDoctor 		: remove, 
						@.attendingNurse 		: remove 
					}

/*
 * Visitors which are not relatives may see the phone number and name only.
 */
policy "visiting relatives access patient data"
permit 
       action.java.shortSig == "PatientRepository.findById(..)"
where 
       "VISITOR" in subject..authority;
transform
		// Constructive template 
		{ 
			"id" 			: resource.id,
			"name" 			: resource.name,
			"phoneNumber"	: resource.phoneNumber
		}


/*
 * Permit doctors to delete patients
 */
policy "doctors and administrators may access the update patient data interface via navigation button"
permit 
       action.java.shortSig == "PatientRepository.deleteById(..)"
where
	("DOCTOR" in subject..authority);

/*
 * Doctors may create new patients. 
 * 
 * In the demo, new patients are also created during application startup. In this case there is no subject defined.
 * So subject == null when the access comes from a system process not linked to a user session.
 * System processes are also permitted to create patients.
 *
 */
policy "doctors and system processes may create patients"
permit 
       action.java.shortSig == "PatientRepository.save(..)"
where
	subject == null || ("DOCTOR" in subject..authority);

/*
 * Only the attending doctor may change diagnosis information or the 
 * attending doctor without any side effects.  
 */
policy "attending doctors may update diagnosis and attending doctors"
permit 
		action.java.shortSig == "PatientRepository.updateDiagnosisTextById(..)"
	|	action.java.shortSig == "PatientRepository.updateIcd11CodeById(..)" 
	|	action.java.shortSig == "PatientRepository.updateAttendingDoctorById(..)" 
where
	("DOCTOR" in subject..authority);
	subject.name == action.arguments[1].<patient.patientRecord>.attendingDoctor;

/*
 * All doctors may change diagnosis information or the attending doctor.
 * But the (original) attending doctor must be notified about the change.
 * 
 * This is an emergency override. The so-called "breaking the glass scenario".  
 */
policy "breaking the glass"
permit 
		action.java.shortSig == "PatientRepository.updateDiagnosisTextById(..)"
	|	action.java.shortSig == "PatientRepository.updateIcd11CodeById(..)" 
	|	action.java.shortSig == "PatientRepository.updateAttendingDoctorById(..)" 
where
	("DOCTOR" in subject..authority);
	var patient = action.arguments[1].<patient.patientRecord>;
obligation
	{
		"type"		: "sendEmail",
		"recipient"	: patient.attendingDoctor,
		"subject"	: "Data of your patient "+(patient.name)+" was changed.",
		"message"	: "Doctor " + subject.name + " changed the data."
	}
	
/*
 * All doctors and nurses may attending nurse field. 
 */
policy "doctors and nurses may change other fields"
permit 
		action.java.shortSig == "PatientRepository.updateAttendingNurseById(..)" 
	|	action.java.shortSig == "PatientRepository.updatePhoneNumberById(..)" 
	|	action.java.shortSig == "PatientRepository.updateNameById(..)" 
	|	action.java.shortSig == "PatientRepository.updatePhoneNumberById(..)" 
	|	action.java.shortSig == "PatientRepository.updateRoomNumberById(..)" 

where
	("DOCTOR" in subject..authority) || ("DOCTOR" in subject..authority);

/*
 * Permit administrators to update/delete patients, but the action SHOULD be logged and an email MUST be sent.
 */
policy "doctors and system processes may create patients"
permit 
       	action.java.shortSig == "PatientRepository.deleteById(..)"
	|	action.java.shortSig =~ "^PatientRepository\\.update.*"
where
	("ADMIN" in subject..authority);

policy "administrators may delete, but it has to be reported"
permit 
       action.java.shortSig == "PatientRepository.deleteById(..)"
where
	("ADMIN" in subject..authority);
obligation
	{
		"type"		: "sendEmail",
		"recipient"	: "Dr. Gregory House <houseMD@ppth.med>",
		"subject"	: "Patient manipulated by administrator.",
		"message"	: ("Administrator" + subject.name + " has manipulated patient: " + action.http.requestedURI)
	}
advice
	{
		"type" 		: "logAccess",
		"message"	: ("Administrator" + subject.name + " has manipulated patient: " + action.http.requestedURI)
	}
