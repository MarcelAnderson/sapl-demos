import geo.*

set "PIL_access_control"
deny-overrides
var deviceTrack = { "deviceID": subject.personalIdentifier, "url": deviceTracking.serverUrl, "credentials": deviceTracking.credentials, "posValidityTimespan": deviceTracking.posValidityTimespan};
var trackedDevice = deviceTrack.<io.sapl.pip.geo.traccar>;


	policy "basic_authorization"
	permit action == "PIL:RETRIEVE"
	where
	  subject.personalIdentifier =~ "^a[0-9]{5}";
	  subject.operationalStatus == param.operationalStatus;
	  isValid(trackedDevice.position); 
	 
	
	policy "restrict_meta_data"
	deny action == "PIL:RETRIEVE" & resource.classification >= param.meta
	where
		var trustedLocations = resToGeometryBag(trustServer.<io.sapl.pip.geo.kml>.geofences.*);
		!(trackedDevice.accuracy <= param.minAccuracy);
		!(trackedDevice.trust < param.minTrust);
		!within(trackedDevice.position, trustedLocations);
		
		
	policy "restrict_restricted_data"
	deny action == "PIL:RETRIEVE" & resource.classification >= param.restricted
	where
		var stations = stationsDB.<io.sapl.pip.geo.postgis>.geofences;
		var airports = airportsDB.<io.sapl.pip.geo.postgis>.geofences;
		var proj = getProjection(param.wgs84, param.webMercator);
		var invProj = getProjection(param.webMercator, param.wgs84);
		var airportArea = project(buffer(project(airports[(resource.depAp)], proj), param.airportBuffer), invProj);
		
		var allowedArea = union(airportArea, stations[(resource.depAp)]);
	    !contains(allowedArea, trackedDevice.position);
			
	
	policy "restrict_confidential_data"
	deny action == "PIL:RETRIEVE" & resource.classification >= param.confidential
	where
		var trackedAC = aircraftTracking.<io.sapl.pip.geo.postgis>.geofences;
		!isWithinGeoDistance(trackedAC[(resource.acReg)], trackedDevice.position, param.maxDistFromAc);
		
				