import io.sapl.demo.shared.pip.PatientPIP as patient

set "accessPolicy"
deny-unless-permit



policy "permit_doctor_read_HRN"
permit
    action.method == "read"
where
  "DOCTOR" in subject..authority;
  resource == "HRN";

policy "permit_doctor_update_HRN"
permit
   action.method == "update"
where
   "DOCTOR" in subject..authority;
   resource == "HRN";
   
   
policy "permit_doctor_update_attending_doctor"
permit
   action.method == "update"
where
   "DOCTOR" in subject..authority;
   resource == "doctor";
   
   
policy "permit_nurse_update_attending_nurse"
permit
   action.method == "update"
where
   "NURSE" in subject..authority;
   resource == "nurse";


policy "get_blacken_and_obligation_test"
permit
    action.method == "getBlackenAndObligation"
where
    "NURSE" in subject..authority;
obligation
    { "type" : "coffee",
      "key1": "Refill Coffee!",
      "coffeeAddiction": "seriously"
    }
transform 
	resource |- filter.blacken(1,0,"\u2588")


policy "permit_attending_doctor_see_diagnosis"
permit
   action.method == "readDiagnosis"
where
   subject.name == resource.attendingDoctor;
advice
   { "type" : "simpleLogging",
     "message" : subject.name + " has looked up the diagnosis of " + resource.name
   }
   

policy "permit_attending_nurse_see_diagnosis"
permit
   action.method == "readDiagnosis"
where
   subject.name == resource.attendingNurse;
obligation
   { "type" : "sendEmail",
     "recipient" : "supervisor@example.org",
     "subject" : "Diagnosis access by nurse",
     "message" : subject.name + " has looked up the diagnosis of " + resource.name
   }
   

/* 
   This is an example to demonstrate a failing obligation. There is no recipient for
   the email, so the EmailObligationHandler will throw an Exception an the  response 
   will be changed to DENY
*/   
policy "permit_nurse_see_diagnosis"
permit
   action.method == "readDiagnosis"
where
   !(subject.name == resource.attendingNurse) &&
   "NURSE" in subject..authority;
obligation
   { "type" : "sendEmail",
     "subject" : "Diagnosis access by nurse",
     "message" : subject.name + " has looked up the diagnosis of " + resource.name
   }



policy "permit_attending_doctor_update_diagnosis"
permit
   action.method == "updateDiagnosis"
where
   subject.name == resource.attendingDoctor;



policy "permit_relative_see_room_number"
permit
   action.method == "viewRoomNumber"
where
  subject.name in resource.id.<patient.related>;

  
policy "permit_doctor_create_patient_profiles"
permit
    action.method == "createPatientProfile"
where
  "DOCTOR" in subject..authority;


  
policy "permit_doctor_Jwt"
permit
    action.method == "GET"
where
  "DOCTOR" in subject.details.authorities;
  resource == "/jwt";
  
