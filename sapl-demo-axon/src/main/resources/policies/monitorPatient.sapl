/*
 * Import the filter library, so that 'blacken' can be used directly instead of using the absolute name 'filter.blacken'.
 */
import filter.*
import log.*

/*
 * In each SAPL document, the top level policy or policy set MUST have a unique name.
 */
set "monitor patient policy set"

/*
 * The 'first-applicable' combination algorithm is used here in oder to avoid 'transformation uncertainty',
 * i.e., multiple policies which return PERMIT but do not agree about transformation of the resource.
 * This algorithm evaluates policies from top to bottom in the document and stops as soon as one policy 
 * yields an applicable result or errors.
 */
first-applicable

/*
 * scope the policy set to be applicable to all authorization subscriptions "Fetch" actions on "patient".
 */
for resource.type == "patient" & action == "Monitor"


/*
 * All doctors have full access to medical data of the patients.
 * All nurses working in the same ward where the patient is hospitalized have full access. 
 */
policy "permit doctors and ward nurses full patient access" 
permit subject.position == "DOCTOR" | (subject.position == "NURSE" & subject.assignedWard == resource.value.ward)


/*
 * All other authenticated staff members have limited access.
 * Also, the medical information must be blackened.  
 */
policy "authenticated users may see filtered" 
permit subject != "anononymous"
obligation
	{
		"type" 		: "filterMessagePayloadContent",
		"actions"   : [
						{ 
						  "type" : "blacken", 
						  "path" : "$.latestIcd11Code", 
						  "discloseLeft": 2
						},
						{ 
						  "type" : "blacken", 
						  "path" : "$.latestDiagnosisText",
						  "discloseLeft": 2						  
						}
                      ]
	}

/*
 * Deny all others access.
 * Upon access, an event must be dispatched to allow auditing of access of the patient data.
 */
policy "deny patient access if no other rule applies" 
deny
obligation "dispatch access attempt event"
