/*
 * Import the filter library, so that 'blacken' can be used directly instead of using the absolute name 'filter.blacken'.
 */
import filter.*

/*
 * In each SAPL document, the top level policy or policy set MUST have a unique name.
 */
set "patients policy set"

/*
 * The 'first-applicable' combination algorithm is used here in oder to avoid 'transformation uncertainty',
 * i.e., multiple policies which return PERMIT but do not agree about transformation of the resource.
 * This algorithm evaluates policies from top to bottom in the document and stops as soon as one policy 
 * yields an applicable result or errors.
 */
first-applicable

/*
 * scope the policy set to be applicable to all auhtorization subscriptions for patient docuemnts.
 */
for resource.type == "patient" & action == "Fetch"

policy "permit doctors full patient access" 
permit subject.position == "DOCTOR"

policy "permit nurses full patient access but log it as an access event" 
permit subject.position == "NURSE"
obligation "dispatch access attempt event"


policy "authenticated users may see filtered" 
permit subject != "anononymous"
transform 
		// Subtractive template with filters removing content
		 resource.value |- 	{ 
								@.latestIcd11Code 			: blacken(2,0,"\u2588"),
								@.latestDiagnosisText 		: blacken(2,0,"\u2588")												
		             		}
