// Import the filter library, so that 'blacken' can be used directly instead of using the absolute name 'filter.blacken'.
import filter.*

set "LaboratoryRepository"

/*
 * The 'first-applicable' combination algorithm is used here in oder to avoid 'transformation uncertainty',
 * i.e., multiple policies which return PERMIT but do not agree about transformation of the resource.
 * This algorithm evaluates policies from top to bottom in the document and stops as soon as one policy 
 * yields an applicable result or errors.
 */
first-applicable

/*
 * This 'for' statement ensures, that the policies are only checked when they are actions, e.g., method calls,
 * in the designated Java class.
 * In this case, spring will indirectly implement the LaboratoryRepository interface and it may not appear directly 
 * in the method signatures, e.g. when using default methods from CrudRepositories. Thus we inspect the class 
 * hierarchy and interfaces implemented by the class to scope the policy set.
 */
for "LaboratoryRepository" in action.java.instanceof..simpleName

// TODO: Erweiterung von Lab in Daten die nur bestimmte Rollen sehen k√∂nnen (z.B. Details nur DOC, Ergebnis (OK/NOK) auch Nurses)

/* #### SYSTEM/ADMIN ACCESS #### */

/*
 * System can perform all actions
 */
policy "system can create/update/delete laboratory"
permit 
       action.java.name == ".*"
where
	("ROLE_SYSTEM" in subject..authority);

/*
 * Administrators can perform all actions but action will be logged
 */
policy "system can create/update/delete laboratory"
permit 
       action.java.name == ".*"
where
	("ROLE_ADMIN" in subject..authority);
	var patientId = resource.patientId
advice
	{
		"type" 		: "logAccess",
		"message"	: ("Administrator " + subject.name + " has manipulated laboratory of patient " + patientId + ": " + action.java.name + "")


/* #### MEDICAL STAFF ACCESS #### */

/*
 * Doctors working in laboratory department have full access on laboratory
 */
policy "laboratory doctors full access on laboratory"
permit 
       action.java.name == ".*"
where
	("ROLE_DOCTOR" in subject..authority) && ("ROLE_LAB" in subject..authority);


/*
 * Medical staff working in laboratory department can create laboratory
 */
policy "medical laboratory staff can create laboratory for patients"
permit 
       action.java.name == "save"
where
	("ROLE_DOCTOR" in subject..authority) || ("ROLE_NURSE" in subject..authority);
	("ROLE_LAB" in subject..authority);

/*
 * Staff working in laboratory department have full read access but without patient info
 */
policy "laboratory staff can access all laboratory entries"
permit 
		action.java.name == "findAll"
	|	action.java.name == "findById"
where
	("ROLE_LAB" in subject..authority);
transform 
		resource |- { 
						@.patientId 	: remove
					}

/*
 * Treating medical staff can access patients laboratory
 */
policy "treating medical staff access on patient laboratory"
permit 
       action.java.name == "findByPatientId"
where
	("ROLE_DOCTOR" in subject..authority) || ("ROLE_NURSE" in subject..authority);
	subject.name == action.arguments[0].<patient.treating>;







