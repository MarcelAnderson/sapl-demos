// Import the filter library, so that 'blacken' can be used directly instead of using the absolute name 'filter.blacken'.
import filter.*

set "PatientRepository"

/*
 * The 'first-applicable' combination algorithm is used here in oder to avoid 'transformation uncertainty',
 * i.e., multiple policies which return PERMIT but do not agree about transformation of the resource.
 * This algorithm evaluates policies from top to bottom in the document and stops as soon as one policy 
 * yields an applicable result or errors.
 */
first-applicable

/*
 * This 'for' statement ensures, that the policies are only checked when they are actions, e.g., method calls,
 * in the designated Java class.
 * In this case, spring will indirectly implement the PatientRepository interface and it may not appear directly 
 * in the method signatures, e.g. when using default methods from CrudRepositories. Thus we inspect the class 
 * hierarchy and interfaces implemented by the class to scope the policy set.
 */
for "PatientRepository" in action.java.instanceof..simpleName


/* #### CREATE ACCESS #### */

/*
 * Medical staff may create new patients. 
 * 
 * In the demo, new patients are also created during application startup. In this case there is no subject defined.
 * So subject == null when the access comes from a system process not linked to a user session.
 * System processes are also permitted to create patients.
 *
 */
policy "medical staff and system processes may create patients"
permit 
       action.java.name == "save"
where
	("ROLE_SYSTEM" in subject..authority) || ("ROLE_DOCTOR" in subject..authority) || ("ROLE_NURSE" in subject..authority);


/* #### DELETE ACCESS #### */

/*
 * Permit system to delete patients
 */
policy "system may delete patient data"
permit 
       action.java.name == "deleteById"
where
	("ROLE_SYSTEM" in subject..authority);

/*
 * Permit treating doctors to delete patients
 */
policy "treating doctors may delete patient data"
permit 
       action.java.name == "deleteById"
where
	("ROLE_DOCTOR" in subject..authority);
	subject.name in action.arguments[0].<patient.treating>;

/*
 * Permit administrators to delete patients, but the action SHOULD be logged and an email MUST be sent.
 */
policy "administrators may delete patients, but it has to be reported"
permit 
       	action.java.name == "deleteById"
where
	("ROLE_ADMIN" in subject..authority);
obligation
	{
		"type"		: "sendEmail",
		"recipient"	: "Dr. Gregory House <houseMD@ppth.med>",
		"subject"	: "Patient manipulated by administrator.",
		"message"	: ("Administrator " + subject.name + " has manipulated patient: " + action.http.requestedURI)
	}
advice
	{
		"type" 		: "logAccess",
		"message"	: ("Administrator " + subject.name + " has manipulated patient: " + action.http.requestedURI)
	}


/* #### READ ACCESS #### */

/*
 * System may search by name or ID with full access on patient record 
 */
policy "system search by name"
permit 
		action.java.name == "findByName"
    |   action.java.name == "findById"
where
	("ROLE_SYSTEM" in subject..authority);


/*
 * This policy enables all authenticated users to see the ID of a patient.
 */
policy "all authenticated users may see patient id"
permit        
	action.java.name == "findByName"
where
	!("ROLE_ANONYMOUS" in subject..authority);
transform 
		// Constructive template 
		{ 
			"id" 			: resource.id,
			"name" 			: resource.name
		}

/*
 * Doctors, nurses, administrators and the system may see the full list of patient IDs.
 */
policy "all authenticated users may see patient list"
permit        
	action.java.name == "findAll"
where
	"ROLE_DOCTOR" in subject..authority || "ROLE_NURSE" in subject..authority || "ROLE_ADMIN" in subject..authority || "ROLE_SYSTEM" in subject..authority;
transform 
		// Constructive template 
		{ 
			"id" 			: resource.id
		}

/*
 * All doctors and nurses have full read access on all patient records.
 */
policy "doctor and nurse access to patient data"
permit 
		action.java.name == "findById"
where 
       "ROLE_DOCTOR" in subject..authority || "ROLE_NURSE" in subject..authority;


/*
 * Visitors which are familiars have full access to patient data.
 * TODO: Begriff f√ºr Patientenvertreteter/Vertrauter
 */
policy "visiting familiars access patient data"
permit 
		action.java.name == "findById"
where 
       "ROLE_VISITOR" in subject..authority;
       subject.name in resource.id.<patient.familiars>; 


/*
 * Visitors which are relatives may see the name, phone number and room number.
 */
policy "visiting relatives access patient data"
permit 
		action.java.name == "findById"
where 
       "ROLE_VISITOR" in subject..authority;
       subject.name in resource.id.<patient.relatives>; 
transform 
		// Constructive template 
		{ 
			"id" 			: resource.id,
			"name" 			: resource.name,
			"phoneNumber"	: resource.phoneNumber
			"roomNumber"	: resource.roomNumber
		}

/*
 * Visitors which are not relatives may see the name and phone number only.
 */
policy "visiting non relatives access patient data"
permit 
		action.java.name == "findById"
where 
       "ROLE_VISITOR" in subject..authority;
transform
		// Constructive template 
		{ 
			"id" 			: resource.id,
			"name" 			: resource.name,
			"phoneNumber"	: resource.phoneNumber
		}



/*
 * Administrators read access patients, however the classification and diagnosis are blackened in parts
 * also administrator access is to be documented.
 */
policy "administrator access to patient data"
permit 
       action.java.name == "findById"
where 
       "ROLE_ADMIN" in subject..authority;
obligation  
		{ 
        	"type" : "logAccess",
			"message" : subject.name + " has accessed patient data (id="+resource.id+") as an administrator."
		}  
transform 
		// filtering with text blackening
		resource |- { 
						@.icd11Code : blacken(2,0,"\u2588"),
						@.diagnosisText : blacken(0,0,"\u2588") 
					}

/* #### UPDATE ACCESS #### */

/*
 * Permit system to update patients
 */
policy "system may update patients"
permit 
		action.java.name =~ "^update.*"
where
	("ROLE_SYSTEM" in subject..authority);


/*
 * Permit administrators to update patients, but the action SHOULD be logged and an email MUST be sent.
 */
policy "administrators may update patients, but it has to be reported"
permit 
		action.java.name =~ "^update.*"
where
	("ROLE_ADMIN" in subject..authority);
obligation
	//TODO loop over all treating doctors
	{
		"type"		: "sendEmail",
		"recipient"	: "Dr. Gregory House <houseMD@ppth.med>",
		"subject"	: "Patient manipulated by administrator.",
		"message"	: ("Administrator " + subject.name + " has manipulated patient: " + action.java.name)
	}
advice
	{
		"type" 		: "logAccess",
		"message"	: ("Administrator " + subject.name + " has manipulated patient: " + action.java.name)
	}


/*
 * Only treating doctors may change diagnosis information without any side effects.  
 */
policy "treating doctors may update diagnosis"
permit 
		action.java.name == "updateDiagnosisTextById"
	|	action.java.name == "updateIcd11CodeById"
where
	("ROLE_DOCTOR" in subject..authority);
	subject.name == action.arguments[1].<patient.treating>;


/*
 * All doctors may change diagnosis information.
 * But the (original) attending doctor must be notified about the change.
 * 
 * This is an emergency override. The so-called "breaking the glass scenario".  
 */
policy "breaking the glass"
permit 
		action.java.name == "updateDiagnosisTextById"
	|	action.java.name == "updateIcd11CodeById" 
where
	("ROLE_DOCTOR" in subject..authority);
	var patient = action.arguments[1].<patient.patientRecord>;
obligation 
	//TODO loop over all treating doctors
	{
		"type"		: "sendEmail",
		"recipient"	: patient.attendingDoctor, 
		"subject"	: "Data of your patient "+(patient.name)+" was changed.",
		"message"	: "Doctor " + subject.name + " changed the data."
	}

/*
 * Medical staff may update personal details.
 */
policy "doctors and nurses may change personal details"
permit 
		action.java.name == "updateNameById"
	|	action.java.name == "updatePhoneNumberById"
	|	action.java.name == "updateGenderById"
	|	action.java.name == "updateDateOfBirthById"
	|	action.java.name == "updateAddressById"
	|	action.java.name == "updateInsuranceById"

where
	("ROLE_DOCTOR" in subject..authority) || ("ROLE_NURSE" in subject..authority);


/*
 * Medical staff may update room number. Treating doctors and nurses must be notified
 */
policy "doctors and nurses may change room number"
permit 
		action.java.name == "updateRoomNumberById" 

where
	("ROLE_DOCTOR" in subject..authority) || ("ROLE_NURSE" in subject..authority);
obligation 
	//TODO send mail to all treating staff
	{
		"type"		: "sendEmail",
		"recipient"	: patient.attendingDoctor,
		"subject"	: "Data of your patient "+(patient.name)+" was changed.",
		"message"	: "Doctor " + subject.name + " changed the data."
	}












