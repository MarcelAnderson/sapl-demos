// Import the filter library, so that 'blacken' can be used directly instead of using the absolute name 'filter.blacken'.
import filter.*

set "MedicationRepository"

/*
 * The 'first-applicable' combination algorithm is used here in oder to avoid 'transformation uncertainty',
 * i.e., multiple policies which return PERMIT but do not agree about transformation of the resource.
 * This algorithm evaluates policies from top to bottom in the document and stops as soon as one policy 
 * yields an applicable result or errors.
 */
first-applicable

/*
 * This 'for' statement ensures, that the policies are only checked when they are actions, e.g., method calls,
 * in the designated Java class.
 * In this case, spring will indirectly implement the MedicationRepository interface and it may not appear directly 
 * in the method signatures, e.g. when using default methods from CrudRepositories. Thus we inspect the class 
 * hierarchy and interfaces implemented by the class to scope the policy set.
 */
for "MedicationRepository" in action.java.instanceof..simpleName

/* #### SYSTEM/ADMIN ACCESS #### */

/*
 * System can perform all actions
 */
policy "system can create/update/delete medication"
permit 
       action.java.name == ".*"
where
	("ROLE_SYSTEM" in subject..authority);

/*
 * Administrators can perform all actions but action will be logged
 */
policy "system can create/update/delete medication"
permit 
       action.java.name == ".*"
where
	("ROLE_ADMIN" in subject..authority);
	var patientId = resource.patientId
advice
	{
		"type" 		: "logAccess",
		"message"	: ("Administrator " + subject.name + " has manipulated medication of patient " + patientId + ": " + action.java.name + "")


/* #### MEDICAL STAFF ACCESS #### */


/*
 * Treating medical staff can access patients medication
 */
policy "treating medical staff access on patient medication"
permit 
       action.java.name == "findByPatientId"
where
	("ROLE_DOCTOR" in subject..authority) || ("ROLE_NURSE" in subject..authority);
	subject.name == action.arguments[0].<patient.treating>;


/*
 * Treating medical staff can update medication status
 */
policy "treating medical staff update medication status"
permit 
       action.java.name == "updateStatusById"
where
	("ROLE_DOCTOR" in subject..authority) || ("ROLE_NURSE" in subject..authority);
	subject.name == action.arguments[1].<patient.treating>;


/*
 * Treating doctors can create medications
 */
policy "treating doctors can create medications"
permit 
       action.java.name == "save"
where
	("ROLE_DOCTOR" in subject..authority);
	subject.name == action.arguments[0].<patient.treating>;

/*
 * Treating nurses can create non-prescription medication
 */
policy "treating nurses can create non-prescription medication"
permit 
       action.java.name == "save"
where
	("ROLE_NURSE" in subject..authority);
	subject.name == action.arguments[0].patientId.<patient.treating>;
	//TODO check against fixed boolean value
	action.arguments[0].prescription == false


/* #### OTHER ACCESS #### */

/*
 * Controlling staff can access medication. Patient Info is blackened
 */
policy "controlling staff can access medication"
permit 
       action.java.name == "findAll"
where
	("ROLE_CONTROLLER" in subject..authority);
transform 
		resource |- { 
						@.patientId 	: remove
					}









