import io.sapl.pip.ClockPolicyInformationPoint as clock
import io.sapl.pip.http.HttpPolicyInformationPoint as http
import io.sapl.functions.TemporalFunctionLibrary as time

set "reactivePolicies"
deny-unless-permit
for action == "read" & resource in ["heartBeatData", "bloodPressureData"]


policy "permit doctor and nurse read time restricted heart beat data"
permit
  resource == "heartBeatData"
where
  "ROLE_DOCTOR" in subject..authority || "ROLE_NURSE" in subject..authority;
  var interval = 1;
  time.localSecond(interval.<clock.ticker>) > 4;


policy "permit doctor and nurse read time restricted blood pressure data"
permit
  resource == "bloodPressureData"
where
  "ROLE_DOCTOR" in subject..authority || "ROLE_NURSE" in subject..authority;
  var interval = 1;
  time.localSecond(interval.<clock.ticker>) < 31 || time.localSecond(interval.<clock.ticker>) > 35;


policy "permit visitor read time restricted heart beat data"
permit
  resource == "heartBeatData"
where
  "ROLE_VISITOR" in subject..authority;
  var url = "http://localhost:8081/rest/time/ticker";
  !(time.localSecond(url.<http.rget>) in [0,1,2,3,10,11,12,13,20,21,22,23,30,31,32,33,40,41,42,43,50,51,52,53]);
