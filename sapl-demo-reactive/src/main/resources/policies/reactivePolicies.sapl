import io.sapl.pip.ClockPolicyInformationPoint as clock
import io.sapl.pip.http.HttpPolicyInformationPoint as http
import io.sapl.functions.TemporalFunctionLibrary as time

set "reactivePolicies"
deny-unless-permit
for action == "read"


policy "permit_doctor_and_nurse_read_time_restricted_heart_beat_data"
permit
  resource == "heartBeatData"
where
  "DOCTOR" in subject..authority || "NURSE" in subject..authority;
  var interval = 1;
  time.localSecond(interval.<clock.ticker>) > 4;


policy "permit_doctor_and_nurse_read_time_restricted_blood_pressure_data"
permit
  resource == "bloodPressureData"
where
  "DOCTOR" in subject..authority || "NURSE" in subject..authority;
  var interval = 1;
  time.localSecond(interval.<clock.ticker>) < 31 || time.localSecond(interval.<clock.ticker>) > 35;


policy "permit_visitor_read_time_restricted_heart_beat_data"
permit
  resource == "heartBeatData"
where
  "VISITOR" in subject..authority;
  var url = "http://localhost:8081/rest/time/ticker";
  !(time.localSecond(url.<http.rget>) in [0,1,2,3,10,11,12,13,20,21,22,23,30,31,32,33,40,41,42,43,50,51,52,53]);
