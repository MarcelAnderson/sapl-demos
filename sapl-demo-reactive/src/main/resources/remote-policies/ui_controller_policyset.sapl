import filter.*

set "UIController"
first-applicable


/*
 * This 'for' statement ensures, that the policies are only checked when they are actions, e.g., method calls,
 * in the designated Java class.
 */
for "UIController" in action.java.instanceof..simpleName

/*
 * This policy enables all authenticated users to see the patient list. The contents of the patient list 
 * page especially the availability of the button to create new patients is controlled 
 * by another policy.
 */
policy "all authenticated users may see patient list"
permit
    action.java.name == "getPatients"
where
	!("ROLE_ANONYMOUS" in subject..authority);

/*
 * This policy enables all authenticated users to see patient detail data. The contents of the patient detail
 * page (which patient attributes are visible / editable) and the availability of the button to save or delete
 * the patient is controlled by other policies.
 */
policy "all authenticated users may access patient records"
permit
    action.java.name == "getPatient"
where
	!("ROLE_ANONYMOUS" in subject..authority);


/*
 * All doctors can create patients on the controller level.
 */
policy "doctors may create new patients"
permit
    action.java.name == "createPatient"
where
	"ROLE_DOCTOR" in subject..authority;


/*
 * All staff members can edit patients under certain conditions defined by other policies.
 */
policy "staff members may edit patients"
permit 
    action.java.name == "updatePatient"
where
	"ROLE_DOCTOR" in subject..authority || "ROLE_NURSE" in subject..authority || "ROLE_ADMIN" in subject..authority;


/*
 * All doctors and administrators can delete patients under certain conditions defined by other policies.
 */
policy "doctors and administrators may delete patients"
permit
    action.java.name == "deletePatient"
where
	"ROLE_DOCTOR" in subject..authority || "ROLE_ADMIN" in subject..authority;
